# shortcut to this dotfiles path is $ZSH
export ZSH=$HOME/.dotfiles

# your project folder that we can `c [tab]` to
export PROJECTS=~/code

# Stash your environment variables in ~/.localrc. This means they'll stay out
# of your main dotfiles repository (which may be public, like this one), but
# you'll have access to them in your scripts.
if [[ -a ~/.localrc ]]
then
  source ~/.localrc
fi

# all of our zsh files
typeset -U config_files
config_files=($ZSH/**/*.zsh)

# load the path files
for file in ${(M)config_files:#*/path.zsh}
do
  source $file
done

# load everything but the path and completion files
for file in ${${config_files:#*/path.zsh}:#*/completion.zsh}
do
  source $file
done

# initialize autocomplete here, otherwise functions won't be loaded
autoload -U compinit
compinit

# load every completion after autocomplete loads
for file in ${(M)config_files:#*/completion.zsh}
do
  source $file
done

unset config_files

export NVM_DIR="/Users/jyria/.nvm"
[ -s "$NVM_DIR/nvm.sh" ] && . "$NVM_DIR/nvm.sh"  # This loads nvm
export RACKET_HOME="/Applications/Racket v6.1.1"
### Added by the Heroku Toolbelt
export HEROKU_HOME="/usr/local/heroku"
export PACKER_HOME="/Users/jyria/opt/packer_0.7.5_darwin_amd64"
export ANDROID_HOME="/Users/jyria/opt/android-sdk-macosx/"
export PATH="$HEROKU_HOME/bin:$RACKET_HOME/bin:$PACKER_HOME/bin:$ANDROID_HOME/tools:$PATH"
